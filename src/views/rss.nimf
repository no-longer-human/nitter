#? stdtmpl(subsChar = '$', metaChar = '#')
## SPDX-License-Identifier: AGPL-3.0-only
#import strutils, xmltree, strformat, options, unicode
#import ../types, ../utils, ../formatters, ../prefs
#import about
#
#proc getTitle(tweet: Tweet; retweet: string; format: RssOutputFormat): string =
#if tweet.pinned: result = "Pinned: "
#elif retweet.len > 0: result = &"RT by @{retweet}: "
#elif tweet.reply.len > 0: result = &"R to @{tweet.reply[0]}: "
#end if
#var text = stripHtml(tweet.text)
##if unicode.runeLen(text) > 32:
##  text = unicode.runeSubStr(text, 0, 32) & "..."
##end if
#result &= xmltree.escape(text)
#if result.len > 0: return
#end if
#if tweet.photos.len > 0 and format.enforceNonEmptyTitle:
#  result &= "Image"
#elif tweet.video.isSome and format.enforceNonEmptyTitle:
#  result &= "Video"
#elif tweet.gif.isSome and format.enforceNonEmptyTitle:
#  result &= "Gif"
#end if
#end proc
#
#proc renderGeneratorAtomTags(cfg: Config): string =
<atom:generator uri="${getUrlPrefix(cfg)}" version="${getVersionString()}">
    Nitter (version ${getVersionString()}) at ${getUrlPrefix(cfg)}
</atom:generator>
#end proc
#
#proc renderCategories(tweet: Tweet): string =
#  for tag in tweet.hashtagsParsed:
     <category>${tag}</category>
#  end for
#end proc
#
#proc renderMediaRating(tweet: Tweet): string =
#  let rating = if tweet.possiblySensitive: "adult" else: "nonadult"
   <media:rating scheme="urn:simple">${rating}</media:rating>
#end proc
#
#proc renderRssTweet(tweet: Tweet; cfg: Config): string =
#let tweet = tweet.retweet.get(tweet)
#let text = replaceUrls(tweet.text, defaultPrefs, absolute=getUrlPrefix(cfg))
<p>${text.replace("\n", "<br>\n")}</p>
#if tweet.quote.isSome and get(tweet.quote).available:
#  let quoteLink = getLink(get(tweet.quote))
<p><a href="${getUrlPrefix(cfg)}${quoteLink}">${cfg.hostname}${quoteLink}</a></p>
#end if
#if tweet.photos.len > 0:
#  for photo in tweet.photos:
<img src="${getUrlPrefix(cfg)}${getPicUrl(photo)}" style="max-width:250px;" />
#  end for
#elif tweet.video.isSome:
<img src="${getUrlPrefix(cfg)}${getPicUrl(get(tweet.video).thumb)}" style="max-width:250px;" />
#elif tweet.gif.isSome:
#  let thumb = &"{getUrlPrefix(cfg)}{getPicUrl(get(tweet.gif).thumb)}"
#  let url = &"{getUrlPrefix(cfg)}{getPicUrl(get(tweet.gif).url)}"
<video poster="${thumb}" autoplay muted loop style="max-width:250px;">
  <source src="${url}" type="video/mp4"></video>
#elif tweet.card.isSome:
#  let card = tweet.card.get()
#  if card.image.len > 0:
<img src="${getUrlPrefix(cfg)}${getPicUrl(card.image)}" style="max-width:250px;" />
#  end if
#end if
#end proc
#
#proc renderRssTweets(tweets: seq[Tweets]; cfg: Config; outputFormat: RssOutputFormat; userId=""): string =
#var links: seq[string]
#for thread in tweets:
#  for tweet in thread:
#    if userId.len > 0 and tweet.user.id != userId: continue
#    end if
#
#    let retweet = if tweet.retweet.isSome: tweet.user.username else: ""
#    let tweet = if retweet.len > 0: tweet.retweet.get else: tweet
#    let link = getLink(tweet)
#    if link in links: continue
#    end if
#    links.add link
      <item atom:lang="${tweet.languageCode}">
        <title>${getTitle(tweet, retweet, outputFormat)}</title>
        <dc:creator>@${tweet.user.username}</dc:creator>
        <atom:author>
          <atom:name>${tweet.user.username}</atom:name>
          <atom:uri>${getUrlPrefix(cfg)}/${tweet.user.username}</atom:uri>
        </atom:author>
        <description><![CDATA[${renderRssTweet(tweet, cfg).strip(chars={'\n'})}]]></description>
        <pubDate>${getRfc822Time(tweet)}</pubDate>
        <guid>${getUrlPrefix(cfg) & link}</guid>
        <link>${getUrlPrefix(cfg) & link}</link>
        ${renderCategories(tweet)}
        ${renderMediaRating(tweet)}
      </item>
#  end for
#end for
#end proc
#
#proc renderTimelineRss*(profile: Profile; cfg: Config; outputFormat: RssOutputFormat; multi=false): string =
#result = ""
#let handle = (if multi: "" else: "@") & profile.user.username
#var title = profile.user.fullname
#if not multi: title &= " / " & handle
#end if
#title = xmltree.escape(title).sanitizeXml
<?xml version="1.0" encoding="UTF-8"?>
<rss version="2.0" xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" xmlns:media="http://search.yahoo.com/mrss/">
  <channel>
    <atom:link href="${getUrlPrefix(cfg)}/${profile.user.username}/rss" rel="self" type="application/rss+xml" />
    <title>${title}</title>
    <link>${getUrlPrefix(cfg)}/${profile.user.username}</link>
    ${renderGeneratorAtomTags(cfg)}
    <description>Twitter feed for user ${handle}</description>
    <language>en-us</language>
    <ttl>40</ttl>
    <image>
      <title>${title}</title>
      <link>${getUrlPrefix(cfg)}/${profile.user.username}</link>
      <url>${getUrlPrefix(cfg)}${getPicUrl(profile.user.getUserPic(style="_400x400"))}</url>
      <width>128</width>
      <height>128</height>
    </image>
#if profile.tweets.content.len > 0:
${renderRssTweets(profile.tweets.content, cfg, outputFormat, userId=profile.user.id)}
#end if
  </channel>
</rss>
#end proc
#
#proc renderListRss*(tweets: seq[Tweets]; list: List; cfg: Config; outputFormat: RssOutputFormat): string =
#let link = &"{getUrlPrefix(cfg)}/i/lists/{list.id}"
#result = ""
<?xml version="1.0" encoding="UTF-8"?>
<rss version="2.0" xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" xmlns:media="http://search.yahoo.com/mrss/">
  <channel>
    <atom:link href="${link}" rel="self" type="application/rss+xml" />
    <title>${xmltree.escape(list.name)} / @${list.username}</title>
    <link>${link}</link>
    ${renderGeneratorAtomTags(cfg)}
    <description>Twitter feed for user ${list.name} by ${list.username}</description>
    <language>en-us</language>
    <ttl>40</ttl>
${renderRssTweets(tweets, cfg, outputFormat)}
 </channel>
</rss>
#end proc
#
#proc renderSearchRss*(tweets: seq[Tweets]; name, param: string; cfg: Config; outputFormat: RssOutputFormat): string =
#let link = &"{getUrlPrefix(cfg)}/search"
#let escName = xmltree.escape(name)
#result = ""
<?xml version="1.0" encoding="UTF-8"?>
<rss version="2.0" xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" xmlns:media="http://search.yahoo.com/mrss/">
  <channel>
    <atom:link href="${link}" rel="self" type="application/rss+xml" />
    <title>Search results for "${escName}"</title>
    <link>${link}</link>
    ${renderGeneratorAtomTags(cfg)}
    <description>Twitter feed for search ${escName}</description>
    <language>en-us</language>
    <ttl>40</ttl>
${renderRssTweets(tweets, cfg, outputFormat)}
  </channel>
</rss>
#end proc
